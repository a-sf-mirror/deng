; Script generated by the Inno Setup Script Wizard.

[Setup]
OutputBaseFilename=deng-1.9.0-beta6.8-setup
VersionInfoVersion=1.9.0.6
AppName=Doomsday Engine
AppVerName=Doomsday Engine 1.9.0-beta6.8
AppPublisher=deng Team
AppPublisherURL=http://sourceforge.net/projects/deng/
AppSupportURL=http://www.dengine.net/dew/
AppUpdatesURL=http://sourceforge.net/projects/deng/
AppCopyright=Copyright (C) 2003-2009 Deng Team
DefaultDirName={pf}\Doomsday
DefaultGroupName=Doomsday Engine
AllowNoIcons=yes
SetupIconFile=..\..\..\snowberry\graphics\snowberry.ico
UninstallIconFile=..\..\..\snowberry\graphics\snowberry.ico
SourceDir=..\..\doomsday\build\win32
OutputDir=..\..\..\distrib\win32\packages
LicenseFile=license.txt
Compression=lzma/max
;Compression=zip/1
SolidCompression=yes
WizardImageFile=installerbg.bmp

[Components]
Name: "Engine"; Description: "Doomsday Engine"; Types: full compact custom; Flags: fixed checkablealone;
Name: "Engine\jDoom"; Description: "jDoom game plugin (DOOM/Ultimate DOOM/DOOMII/Final DOOM)"; Types: full compact
Name: "Engine\jHeretic"; Description: "jHeretic game plugin (Heretic/Heretic: Shadow of the Serpent Riders)"; Types: full compact
Name: "Engine\jHexen"; Description: "jHexen game plugin (Hexen/Hexen: Death Kings of the Dark Citadel)"; Types: full compact
Name: "Launcher"; Description: "Doomsday Engine launcher (front end)"; Types: full

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Dirs]
Name: "{app}\snowberry"
Name: "{app}\snowberry\profiles"
Name: "{app}\snowberry\addons"
Name: "{app}\snowberry\graphics"
Name: "{app}\snowberry\conf"
Name: "{app}\snowberry\lang"
Name: "{app}\snowberry\plugins"
Name: "{app}\doc"

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
; Binaries
Source: "Bin\Doomsday.exe"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine
Source: "Bin\ds*.dll"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine
Source: "Bin\dp*.dll"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine
Source: "Bin\jDoom.dll"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine\jDoom
Source: "Bin\jHeretic.dll"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine\jHeretic
Source: "Bin\jHexen.dll"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine\jHexen

; Libraries
Source: "SDL.dll"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine
Source: "SDL_net.dll"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine
Source: "eax.dll"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine
Source: "libogg-0.dll"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine
Source: "libvorbis-0.dll"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine
Source: "libvorbisfile-3.dll"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine
Source: "mikmod.dll"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine
Source: "SDL_mixer.dll"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine
Source: "smpeg.dll"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine
Source: "..\..\external\lzss\win32\lzss.dll"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine
Source: "..\..\external\zlib\win32\zlib1.dll"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine
Source: "..\..\external\libpng\win32\libpng13.dll"; DestDir: "{app}\bin"; Flags: ignoreversion; Components: Engine

; Snowberry
Source: "..\..\..\snowberry\dist\*"; DestDir: "{app}\snowberry"; Flags: ignoreversion; Components: Launcher
Source: "..\..\..\snowberry\graphics\*.bmp"; DestDir: "{app}\snowberry\graphics"; Components: Launcher
Source: "..\..\..\snowberry\graphics\*.png"; DestDir: "{app}\snowberry\graphics"; Components: Launcher
Source: "..\..\..\snowberry\graphics\*.jpg"; DestDir: "{app}\snowberry\graphics"; Components: Launcher
Source: "..\..\..\snowberry\graphics\*.ico"; DestDir: "{app}\snowberry\graphics"; Components: Launcher
Source: "..\..\..\snowberry\lang\*.lang"; DestDir: "{app}\snowberry\lang"; Components: Launcher
Source: "..\..\..\snowberry\plugins\*.py"; DestDir: "{app}\snowberry\plugins"; Excludes: "observer.py"; Components: Launcher
Source: "..\..\..\snowberry\plugins\tab30.plugin\Contents\tab30\*.py"; DestDir: "{app}\snowberry\plugins\tab30.plugin\Contents\tab30"; Components: Launcher
Source: "..\..\..\snowberry\profiles\*.prof"; DestDir: "{app}\snowberry\profiles"; Components: Launcher
Source: "..\..\..\snowberry\conf\*.conf"; DestDir: "{app}\snowberry\conf"; Components: Launcher
; Documentation
Source: "..\..\plugins\jdoom\doc\LICENSE"; DestDir: "{app}\doc\jdoom"; Components: Engine\jDoom
Source: "..\..\plugins\jheretic\doc\LICENSE"; DestDir: "{app}\doc\jheretic"; Components: Engine\jHeretic
Source: "..\..\plugins\jhexen\doc\LICENSE"; DestDir: "{app}\doc\jhexen"; Components: Engine\jHexen

; Resources
Source: "doomsday.pk3"; DestDir: "{app}\data"; Components: Engine
Source: "..\..\engine\data\graphics\background.pcx"; DestDir: "{app}\data\graphics"; Components: Engine
Source: "..\..\engine\data\graphics\loading1.png"; DestDir: "{app}\data\graphics"; Components: Engine
Source: "..\..\engine\data\graphics\loading2.png"; DestDir: "{app}\data\graphics"; Components: Engine
Source: "..\..\engine\data\graphics\logo.png"; DestDir: "{app}\data\graphics"; Components: Engine
Source: "jdoom.pk3"; DestDir: "{app}\data\jdoom"; Components: Engine\jDoom
Source: "jheretic.pk3"; DestDir: "{app}\data\jheretic"; Components: Engine\jHeretic
Source: "jhexen.pk3"; DestDir: "{app}\data\jhexen"; Components: Engine\jHexen

[INI]
Filename: "{app}\doomsday.url"; Section: "InternetShortcut"; Key: "URL"; String: "http://sourceforge.net/projects/deng/"

[Icons]
Name: "{group}\Doomsday Engine"; Filename: "{app}\snowberry\snowberry.exe"; WorkingDir: "{app}\snowberry"; Flags: createonlyiffileexists
Name: "{group}\{cm:ProgramOnTheWeb,Doomsday Engine}"; Filename: "{app}\doomsday.url"
Name: "{group}\{cm:UninstallProgram,Doomsday Engine}"; Filename: "{uninstallexe}"
Name: "{userdesktop}\Doomsday Engine"; Filename: "{app}\snowberry\snowberry.exe"; WorkingDir: "{app}\snowberry"; Tasks: desktopicon; Flags: createonlyiffileexists

[Run]
Filename: "{app}\snowberry\snowberry.exe"; Description: "{cm:LaunchProgram,Doomsday Engine}"; WorkingDir: "{app}\snowberry"; Flags: nowait postinstall skipifdoesntexist skipifsilent

[UninstallDelete]
Type: files; Name: "{app}\doomsday.url"
Type: files; Name: "{app}\snowberry\plugins\tab30.plugin\Contents\tab30\*.pyc"
Type: files; Name: "{app}\snowberry\plugins\*.pyc"
Type: files; Name: "{app}\snowberry\*.log"
Type: dirifempty; Name: "{app}\snowberry\plugins\tab30.plugin"
Type: dirifempty; Name: "{app}\snowberry\plugins"
Type: dirifempty; Name: "{app}\snowberry"
Type: dirifempty; Name: "{app}"
